//Buildscript: Armory
// Load the properties of this project.
ext.configFile = file "build.properties"

//Reference the properties inside the project:
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

//ForgeGradle BuildScript.
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

//The repositories in which the dependencies are stored:
repositories {
    mavenCentral()
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { //The repo to get NEI etc
        name 'CB FS'
        url 'http://chickenbones.net/maven'
    }
    maven { //The repo to get TiC
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven { //Forges own repo
        name 'ForgeFS'
        url 'http://files.minecraftforge.net/maven'
    }
    maven { //My own repo (used for uploading and dependency)
        name 'OrionMavenRepo'
        url 'http://mavenrepo.orionminecraft.com/'
    }
}

//Apply the gradle plugins.
apply plugin: 'forge'
apply plugin: 'maven'

//These configurations are needed for uploading to a MavenRepo:
configurations {
    deployerJars
}

//Initializing the mod environment
version = config.mod_version.toString() + "-" + System.getenv().TRAVIS_BUILD_NUMBER.toString()
if(System.getenv().TRAVIS_BRANCH.toString().contains("Development"))
{
    version = version + "-SNAPSHOT"
}
group= "com.Orion.Armory"
archivesBaseName = "Armory"

//Points Gradle (and ForgeGradle) to the right directories:
sourceSets {
    main {
        resources {
            srcDir 'Resources'
        }
        java {
            srcDir 'Source/Armory'
        }
    }
}

//Sets up the currect versions for minecraft and Forge
minecraft {
    version = config.minecraft_version.toString() + "-" + config.forge_version
    runDir = "run/assets"

    //Replacing stuff inside the code:
    replace "@VERSION@", project.version
    replaceIn "Armory.java"
}

//Tells gradle which dependencies are needed for gradle and project
dependencies {
    //Mod dependencies
    //compile "com.Orion.OrionsBelt:OrionsBelt:"+ config.orionsbelt_version
    compile "mantle:Mantle:${config.minecraft_version}-${config.mantle_version}:deobf"
    compile "tconstruct:TConstruct:${config.minecraft_version}-${config.tconstruct_version}:deobf"

    //Maven uploader
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

//This will process all the resources used during build, and needed for running the project
processResources
{
    //This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    //Replaces stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    //Copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//The following function comes with thanks to AbrarSyed:
//Copying the coremod dependencies from the classpath to the mods directory so the system can load them in a Dev Env.
//This is not executed during build!
task copyChicken(type: Copy){
    from configurations.compile
    include "**/*Chicken*.jar", "**/*NotEnoughItems*.jar"
    exclude "**/CodeChickenLib*" // because CCC downloads it anyways.. -_-
    into file(minecraft.runDir + "/mods") // parallel to the assets dir
}

//External task for settingup the a dev environment to develop Armory inc. NEI and WAILA 
task createDevSetup{
    description "Creates a full decompiled and ready to go (as far as possible) dev environment."
}

//Set the dependencies properly for the createDevSetup task.
tasks.createDevSetup.dependsOn("setupDevWorkspace", "setupDecompWorkspace", "copyChicken")

//Function to upload completed project to the maven repo.
//TODO: Implement uploading to CurseForge
uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        if(System.getenv().TRAVIS_BRANCH.toString().contains("Development"))
        {
            snapshotRepository(url: "ftp://mavenrepo.orionminecraft.com"){
                authentication(userName: "mavenrepo.orionminecraft.com|" + System.getenv().FTPUserName.toString(), password: System.getenv().FTPPassword.toString())
            }
        }
        else {
            repository(url: "ftp://mavenrepo.orionminecraft.com") {
                authentication(userName: "mavenrepo.orionminecraft.com|" + System.getenv().FTPUserName.toString(), password: System.getenv().FTPPassword.toString())
            }
        }
    }
}

task('createGithubBranches') << {
    if (System.getenv().TRAVIS_BRANCH.toString().contains("Development"))
    {
        logger.lifecycle("Uploading code to the corresponding Minecraft version branch. Creating branch if needed.")
        logger.lifecycle("Creating local branch.")
        def createLocalBranchCMD = "git checkout -b Minecraft-\" + config.minecraft_version.toString()"
        def createLocalProg = createLocalBranchCMD.execute();
        createLocalProg.waitFor()
        logger.lifecycle("Result of the branch creation:")
        logger.lifecycle(createLocalProg.text.trim())

        logger.lifecycle("Uploading force push to repo.")
        def setUrlCMD = "git config remote.origin.url https://" + System.getenv().GitUsername.toString() + ":" + System.getenv().GitPassword.toString() + "@github.com/OrionDevelopment/Armory"
        def setURLProg = setUrlCMD.execute()
        setURLProg.waitFor()
        logger.lifecycle("Result of the url change:")
        logger.lifecycle(setURLProg.text.trim())

        def cmd = "git push origin -f Minecraft-" + config.minecraft_version.toString()
        def prog = cmd.execute()
        prog.waitFor()
        logger.lifecycle("Result of the upload:")
        logger.lifecycle(cmd.text.trim())

        logger.lifecycle("Mirrored the code the corresponding minecraft version branch. Current MC Version: " + config.minecraft_version.toString() + ". Branch name: Minecraft-" + config.minecraft_version.toString() + ".")
    }
}

//The external task that executes the uploadAtchives function.
task('uploadJars', dependsOn: uploadArchives) {
    description = "uploads JARs"
}

